program ::= function+

function ::= 'function' <id> '(' params? ')' ':' <id> block

params ::= <id> ':' <id> (',' params)*

expr ::= block | assign

block ::= '{' exprs? '}'

exprs ::= expr (';' exprs)*

assign ::=
      lvalue ('=' assign)?
    | cond

cond ::= addsub (('<'|'>'|'=='|'!='|'<='|'>=') addsub)*

addsub ::= muldiv (('+'|'-') muldiv)*

muldiv ::= primary (('*'|'/') primary)*

primary ::=
    def
    | if_else
    | while
    | fcall
    | <integer>
    | <float_number>
    | 'true'
    | 'false'
    | 'null'
    | '-' primary
    | '!' primary
    | '(' expr ')'
    | lvalue

lvalue ::= <id>

def ::= 'let' lvalue (':' <id>)? '=' expr

if_else ::= 'if' expr block ('else' else_tail)?

else_tail ::=
      if_else
    | block

while ::= 'while' expr block

fcall ::= <id> '(' args? ')'

args ::= expr (',' expr)*
