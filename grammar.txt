program ::= functions

functions ::= function+

function ::= 'function' <id> '(' optparams ')' ':' <id> block

optparams ::= params?

params ::= <id> ':' <id> (',' params)*

expr ::= block | cond

block ::= '{' optexprs '}'

optexprs ::= exprs?

exprs ::= expr (';' exprs)*

cond ::= addsub (('<'|'>'|'=='|'!='|'<='|'>=') addsub)*

addsub ::= muldiv (('+'|'-') muldiv)*

muldiv ::= primary (('*'|'/') primary)*

primary ::=
      def
    | assign
    | if_else
    | while
    | fcall
    | <id>
    | <integer>
    | <float_number>
    | 'true'
    | 'false'
    | 'null'
    | '-' '(' expr ')'
    | '!' '(' expr ')'
    | '(' expr ')'

def ::= 'let' <id> ':' <id> '=' expr

assign ::= <id> '=' expr

if_else ::= 'if' '(' expr ')' expr ('else' expr)?
    
while ::= 'while' '(' expr ')' expr

fcall ::= <id> '(' optargs ')'

optargs ::= args?

args ::= expr (',' args)*
