program ::= functions

functions ::= function optfunctions

optfunctions ::= functions | <empty>

function ::= 'function' <id> '(' optparams ')' ':' <id> block

optparams ::= params | <empty>

params ::= <id> ':' <id> params_rest

params_rest ::=
      ',' params
    | <empty>

block ::=
      expr
    | '{' optexprs '}'

optexprs ::= exprs | <empty>

exprs ::= expr exprs_rest

exprs_rest ::=
      ';' exprs
    | <empty>

expr ::=
      def
    | assign
    | if
    | while
    | fcall
    | cond

def ::= 'let' <id> ':' <id> '=' expr

assign ::= <id> '=' expr

if ::= 'if' '(' expr ')' block
    
while ::= 'while' '(' expr ')' block

fcall ::= <id> '(' optargs ')'

optargs ::= args | <empty>

args ::= expr args_rest

args_rest ::=
      ',' args
    | <empty>

cond ::= addsub cond_rest

cond_rest ::=
      '<' addsub cond_rest
    | '>' addsub cond_rest
    | '==' addsub cond_rest
    | '!=' addsub cond_rest
    | '<=' addsub cond_rest
    | '>=' addsub cond_rest
    | <empty>

addsub ::= muldiv addsub_rest

addsub_rest ::=
      '+' muldiv addsub_rest
    | '-' muldiv addsub_rest
    | <empty>

muldiv ::= factor muldiv_rest

muldiv_rest ::=
      '*' factor muldiv_rest
    | '/' factor muldiv_rest
    | <empty>

factor ::=
      <id>
    | <integer>
    | <float_number>
    | true
    | false
    | null
    | '-' '(' expr ')'
    | '!' '(' expr ')'
    | '(' expr ')'
