function -> 'function' <id> '(' optparams ')' ':' <id> block

optparams -> params | <empty>

params -> <id> ':' <id> params_rest

params_rest ->
      ',' params
    | <empty>

block ->
      '{' optexprs '}'
    | expr

optexprs -> exprs | <empty>

exprs ->
      expr
    | expr ';' exprs

expr ->
      def
    | assign
    | op
    | if
    | while
    | fcall

def -> 'let' <id> def_rest

def_rest ->
      ':' <id> '=' expr
    | '=' expr

assign -> <id> '=' expr

if -> 'if' '(' expr ')' block optelse

optelse ->
      'else' block
    | <empty>
    
while -> 'while' '(' expr ')' block

fcall -> <id> '(' optargs ')'

optargs -> args | <empty>

args -> expr args_rest

args_rest ->
      ',' args
    | <empty>

op -> addsub op_rest

op_rest ->
      '<' addsub
    | '>' addsub
    | '==' addsub
    | '!=' addsub
    | '<=' addsub
    | '>=' addsub
    | <empty>

addsub -> muldiv addsub_rest

addsub_rest ->
      '+' muldiv
    | '-' muldiv
    | <empty>

muldiv -> factor muldiv_rest

muldiv_rest ->
      '*' factor
    | '/' factor
    | <empty>

factor ->
      <id>
    | <number>
    | true
    | false
    | '-' expr
    | '!' expr
    | '(' expr ')'
