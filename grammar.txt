program ::= function+

function ::= 'function' <id> '(' params? ')' ':' <id> block

params ::= <id> ':' <id> (',' params)*

expr ::= block | cond

block ::= '{' exprs? '}'

exprs ::= expr (';' exprs)*

cond ::= addsub (('<'|'>'|'=='|'!='|'<='|'>=') addsub)*

addsub ::= muldiv (('+'|'-') muldiv)*

muldiv ::= primary (('*'|'/') primary)*

primary ::=
      def
    | assign
    | if_else
    | while
    | fcall
    | <id>
    | <integer>
    | <float_number>
    | 'true'
    | 'false'
    | 'null'
    | '-' primary
    | '!' primary
    | '(' expr ')'

def ::= 'let' <id> ':' <id> '=' expr

assign ::= <id> '=' expr

if_else ::= 'if' '(' expr ')' block ('else' else_tail)?

else_tail ::=
      if_else
    | block

while ::= 'while' '(' expr ')' block

fcall ::= <id> '(' args? ')'

args ::= expr (',' expr)*
