program -> functions

functions -> function optfunctions

optfunctions -> functions | <empty>

function -> 'function' <id> '(' optparams ')' ':' <id> block

optparams -> params | <empty>

params -> <id> ':' <id> params_rest

params_rest ->
      ',' params
    | <empty>

block ->
      expr
    | '{' optexprs '}'

optexprs -> exprs | <empty>

exprs -> expr exprs_rest

exprs_rest ->
      ';' exprs
    | <empty>

expr ->
      def
    | assign
    | if
    | while
    | fcall
    | op

def -> 'let' <id> def_rest

def_rest ->
      ':' <id> '=' expr
    | '=' expr

// FIRST(fcall) == FIRST(assign) == FIRST(op)
assign -> <id> '=' expr

if -> 'if' '(' expr ')' block optelse

optelse ->
      'else' block
    | <empty>
    
while -> 'while' '(' expr ')' block

// FIRST(fcall) == FIRST(assign) == FIRST(op)
fcall -> <id> '(' optargs ')'

optargs -> args | <empty>

args -> expr args_rest

args_rest ->
      ',' args
    | <empty>

// FIRST(fcall) == FIRST(assign) == FIRST(op)
op -> addsub cond_rest

cond_rest ->
      '<' addsub
    | '>' addsub
    | '==' addsub
    | '!=' addsub
    | '<=' addsub
    | '>=' addsub
    | <empty>

addsub -> muldiv addsub_rest

addsub_rest ->
      '+' muldiv
    | '-' muldiv
    | <empty>

muldiv -> factor muldiv_rest

muldiv_rest ->
      '*' factor
    | '/' factor
    | <empty>

// FIRST(fcall) == FIRST(assign) == FIRST(op)
factor ->
      <id>
    | <integer>
    | <float_number>
    | true
    | false
    | '-' expr
    | '!' expr
    | '(' expr ')'
